plugins {
	id 'eclipse'
	id 'idea'
	id 'net.minecraftforge.gradle' version '5.1.+'
	id 'org.spongepowered.mixin' version '0.7.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.0'
group = 'bl4ckscor3.mod.scwthitaddon'
archivesBaseName = 'scwthitaddon-1.16.5'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	duplicatesStrategy = 'include'

	//replace ${version} in mods.toml
	inputs.property 'version', project.version

	from(sourceSets.main.resources.srcDirs) {
		include 'META-INF/mods.toml'

		expand 'version': project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'META-INF/mods.toml'
	}
}

minecraft {
    mappings channel: 'parchment', version: '2022.03.06-1.16.5'

	runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'
            args '-mixin.config=scwthitaddon.mixins.json'

            mods {
                scwthitaddon {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
            args '-mixin.config=scwthitaddon.mixins.json'

            mods {
                scwthitaddon {
                    source sourceSets.main
                }
            }
        }
	}
}

repositories {
    maven {
    	url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'
    annotationProcessor 'org.spongepowered:mixin:0.8.3:processor'

    implementation fg.deobf("curse.maven:security-craft-64760:4795140")
    implementation fg.deobf("curse.maven:wthit-forge-455982:3831595")
}

mixin {
    add sourceSets.main, "scwthitaddon.refmap.json"
}

jar {
    manifest {
        attributes(["Specification-Title": "SecurityCraft WTHIT Addon",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "SecurityCraft WTHIT Addon",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3",
                    "MixinConfigs": "scwthitaddon.mixins.json"])
    }
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}